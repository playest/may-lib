import fr.irit.smac.may.lib.classic.interfaces.CreateClassic
import fr.irit.smac.may.lib.classic.interfaces.CreateNamed
import fr.irit.smac.may.lib.classic.interfaces.CreateRemoteClassic
import fr.irit.smac.may.lib.classic.namedpub.NamedPublishMASFactory
import fr.irit.smac.may.lib.components.controlflow.SequentialDispatcher
import fr.irit.smac.may.lib.components.interactions.DirRefAsyncReceiver
import fr.irit.smac.may.lib.components.interactions.MapRefAsyncReceiver
import fr.irit.smac.may.lib.components.interactions.MapRefValuePublisher
import fr.irit.smac.may.lib.components.interactions.directreferences.DirRef
import fr.irit.smac.may.lib.components.interactions.interfaces.Observe
import fr.irit.smac.may.lib.components.meta.Forward
import fr.irit.smac.may.lib.components.remote.messaging.receiver.RemoteAgentRef
import fr.irit.smac.may.lib.components.remote.messaging.receiver.RemoteReceiver
import fr.irit.smac.may.lib.components.remote.place.Place
import fr.irit.smac.may.lib.components.remote.place.Placed
import fr.irit.smac.may.lib.components.scheduling.Clock
import fr.irit.smac.may.lib.components.scheduling.ExecutorServiceWrapper
import fr.irit.smac.may.lib.components.scheduling.Scheduler
import fr.irit.smac.may.lib.components.scheduling.SchedulingControllerGUI
import fr.irit.smac.may.lib.interfaces.Do
import fr.irit.smac.may.lib.interfaces.Pull
import fr.irit.smac.may.lib.interfaces.Push
import fr.irit.smac.may.lib.interfaces.Send
import java.util.concurrent.Executor

namespace fr.irit.smac.may.lib.classic {

	namespace namedPublish {
		
		
		component ObservedBehaviour {
			provides cycle: Do
			requires changeValue: Push[Integer]
		}
		
		component ObserverBehaviour[Ref] {
			provides cycle: Do
			requires observe: Observe[Integer,Ref]
		}
		
		ecosystem NamedPublishMAS {
			
			provides create: NamedPublishMASFactory
			
			part observeds: MapRefValuePublisher[Integer,String]
			
			part executor: ExecutorServiceWrapper
			part schedule: Scheduler {
				bind executor to executor.executor
			}
			part clock: Clock {
				bind sched to executor.executor
				bind tick to schedule.tick
			}
			part gui: SchedulingControllerGUI {
				bind control to clock.control
			}

			species Observed(name: String, implem: ObservedBehaviour) {

				use sched: schedule.Scheduled {
					bind cycle to beh.cycle
				}
				
				part beh: ObservedBehaviour {
					bind changeValue to observed.set 
				}
				
				use observed: observeds.PublisherPush(name)
				
			}
			
			species Observer(beha: ObserverBehaviour[String]) {
				
				use sched: schedule.Scheduled {
					bind cycle to beh.cycle
				}
				
				part beh: ObserverBehaviour[String] {
					bind observe to observer.observe
				}
				use observer: observeds.Observer
				
			}
			
		}
	}

	namespace named {
		
		
		ecosystem ClassicNamed[Msg] {
			
			provides send: Send[Msg, String] = receive.deposit
			provides create: CreateNamed[Msg, String]
						
			part fact: Forward[CreateNamed[Msg, String]] {
				bind forwardedPort to create
			}
			
			part receive: MapRefAsyncReceiver[Msg,String]
			
			part executor: ExecutorServiceWrapper
			
			species ClassicNamedAgent(beh: ClassicNamedBehaviour[Msg,String], name: String) {
				part arch: ClassicNamedAgentComponent[Msg, String] {
					bind send to ss.send
					bind executor to s.executor
					bind die to s.stop
					bind create to f.forwardedPort
					bind me to receive.me
				}

				use s: executor.Executing

				use f: fact.Caller

				use receive: receive.Receiver(name) {
					bind put to arch.put
				}
				
				use ss: receive.Sender
			}
		}
		
		component ClassicNamedBehaviour[Msg, Ref] {
			provides cycle: Push[Msg]
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires die: Do
			requires create: CreateNamed[Msg, Ref]
		}
		
		component ClassicNamedAgentComponent[Msg, Ref] {
			
			provides put: Push[Msg] = dispatcher.dispatch
			
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires executor: Executor
			requires die: Do
			requires create: CreateNamed[Msg, Ref]

			part dispatcher: SequentialDispatcher[Msg] {
				bind executor to executor
				bind handler to beh.cycle
			}
			
			part beh: ClassicNamedBehaviour[Msg, Ref] {
				bind send to send
				bind me to me
				bind die to die
				bind create to create
			}

		}
	}

	namespace local {
		
		component ClassicBehaviour[Msg, Ref] {
			provides cycle: Push[Msg]
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires die: Do
			requires create: CreateClassic[Msg, Ref]
		}
		
		ecosystem Classic[Msg] {

			provides send: Send[Msg, DirRef] = receive.deposit
			provides create: CreateClassic[Msg, DirRef]
			provides stop: Do = executor.stop
			
			part receive: DirRefAsyncReceiver[Msg]

			part fact: Forward[CreateClassic[Msg, DirRef]] {
				bind forwardedPort to create
			}

			part executor: ExecutorServiceWrapper

			species ClassicAgent(beh: ClassicBehaviour[Msg,DirRef], name: String) {
				provides me: Pull[DirRef] = receive.me
				
				part arch: ClassicAgentComponent[Msg, DirRef] {
					bind send to ss.send
					bind me to receive.me
					bind executor to s.executor
					bind die to s.stop
					bind create to f.forwardedPort
				}

				use s: executor.Executing

				use f: fact.Caller
				
				use receive: receive.Receiver(name) {
					bind put to arch.put
				}

				use ss: receive.Sender
			}
		}

		component ClassicAgentComponent[Msg, Ref] {
			provides put: Push[Msg] = dispatcher.dispatch
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires executor: Executor
			requires die: Do
			requires create: CreateClassic[Msg, Ref]

			part dispatcher: SequentialDispatcher[Msg] {
				bind executor to executor
				bind handler to beh.cycle
			}
			part beh: ClassicBehaviour[Msg, Ref] {
				bind send to send
				bind me to me
				bind die to die
				bind create to create
			}
		}
	}

	namespace remote {

		component RemoteClassicBehaviour[Msg, Ref] {
			provides cycle: Push[Msg]
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires die: Do
			requires create: CreateRemoteClassic[Msg, Ref]
		}

		component RemoteClassicAgentComponent[Msg, Ref] {
			provides put: Push[Msg] = dispatcher.dispatch
			requires send: Send[Msg, Ref]
			requires me: Pull[Ref]
			requires executor: Executor
			provides die: Do
			requires stopExec: Do
			requires stopReceive: Do
			requires create: CreateRemoteClassic[Msg, Ref]

			part dispatcher: SequentialDispatcher[Msg] {
				bind executor to executor
				bind handler to beh.cycle
			}
			part beh: RemoteClassicBehaviour[Msg, Ref] {
				bind send to send
				bind me to me
				bind die to die
				bind create to create
			}
		}

		ecosystem RemoteFactory[Msg, Ref] {
			species Agent {
				provides create: CreateRemoteClassic[Msg, Ref]
			}
			requires infraCreate: CreateRemoteClassic[Msg, Ref]
			provides factCreate: CreateRemoteClassic[Msg, Ref]
			requires thisPlace: Pull[Place]
		}
		


		ecosystem RemoteClassic[Msg] {

			provides send: Send[Msg, RemoteAgentRef] = remReceive.deposit
			provides thisPlace: Pull[Place] = placed.thisPlace
			provides create: CreateRemoteClassic[Msg, RemoteAgentRef]
			
			part sender: Forward[Send[Msg, RemoteAgentRef]] {
				bind forwardedPort to remReceive.deposit
			}

			part receive: DirRefAsyncReceiver[Msg]

			part placed: Placed

			part remReceive: RemoteReceiver[Msg, DirRef] {
				bind localDeposit to receive.deposit
				bind myPlace to placed.thisPlace
			}

			part fact: RemoteFactory[Msg, RemoteAgentRef] {
				bind infraCreate to create
				bind thisPlace to placed.thisPlace
			}

			part executor: ExecutorServiceWrapper

			species ClassicAgent(beh: RemoteClassicBehaviour[Msg, RemoteAgentRef], name: String) {
			//species ClassicAgent(name: String) {
				provides ref: Pull[RemoteAgentRef] = rr.me
				part arch: RemoteClassicAgentComponent[Msg, RemoteAgentRef] {
					bind send to ss.forwardedPort
					bind me to rr.me
					bind stopExec to s.stop
					bind stopReceive to rr.disconnect
					bind executor to s.executor
					bind create to f.create
				}
				use p: placed.Agent
				use f: fact.Agent
				use s: executor.Executing
				use r: receive.Receiver(name) {
					bind put to arch.put
				}
				use ss: sender.Caller
				use rr: remReceive.Agent {
					bind localMe to r.me
				}
			}
		}
	}
}